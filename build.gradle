/*
 * (c) Copyright 2021 Palantir Technologies Inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        mavenCentral() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
        gradlePluginPortal() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
    }

    dependencies {
        classpath 'com.palantir.gradle.externalpublish:gradle-external-publish-plugin:1.19.0'
        classpath 'com.palantir.gradle.failure-reports:gradle-failure-reports:1.13.0'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:2.27.0'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:3.1.0'
        classpath 'com.palantir.gradle.jdks:gradle-jdks:0.48.0'
        classpath 'com.palantir.gradle.jdkslatest:gradle-jdks-latest:0.15.0'
    }
}

apply plugin: 'com.palantir.external-publish-dist'
apply plugin: 'com.palantir.failure-reports'
apply plugin: 'com.palantir.git-version'
apply plugin: 'com.palantir.jdks'
apply plugin: 'com.palantir.jdks.latest'
apply plugin: 'com.palantir.consistent-versions'

group 'com.palantir.conjure.rust'
version gitVersion()

task distTar(type: Tar) {
    compression Compression.GZIP

    into ('bin') {
        from 'dist'
    }

    [
        'x86_64-unknown-linux-musl',
        'x86_64-apple-darwin',
        'x86_64-pc-windows-msvc',
        'aarch64-apple-darwin',
    ].each { target -> 
        into ("bin/$target") {
            from "target/$target/release/conjure-rust"
            from "target/$target/release/conjure-rust.exe"
        }
    }

    into archiveFileName.map { s -> s[0..<s.lastIndexOf('.')] }
}

jdks {
    daemonTarget = 17
}
